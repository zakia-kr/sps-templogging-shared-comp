// To match the android projects because Sonar can't figure things out.
sourceSets {
    main {
        output.resourcesDir = file('build/intermediates/javac/debug/classes')
        java.outputDir = file('build/intermediates/javac/debug/classes')
    }
    test {
        output.resourcesDir = file('build/intermediates/javac/debugUnitTest/classes')
        java.outputDir = file('build/intermediates/javac/debugUnitTest/classes')
    }
}

tasks.create(name: "testDebugUnitTestCoverage", type: JacocoReport, dependsOn: ['test']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for the ${project.name.capitalize()} build"

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }

    def fileFilter = [
            '**/*$Lambda$*.*',
    ]

    classDirectories.setFrom(files([
            fileTree(dir: "${buildDir}/tmp/kotlin-classes/main", excludes: fileFilter),
            fileTree(dir: "${buildDir}/intermediates/javac/debug/classes", excludes: fileFilter),
    ]))

    def coverageSourceDirs = [
            "$project.projectDir/src/main/java"
    ]
    additionalSourceDirs.setFrom(files(coverageSourceDirs))
    sourceDirectories.setFrom(files(coverageSourceDirs))

    executionData(files([
            "$project.buildDir/jacoco/test.exec"
    ]))
}